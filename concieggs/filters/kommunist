#!/usr/bin/env perl
use 5.012;
use strict;
use warnings;
use utf8::all;
use IPC::System::Simple qw(systemx EXIT_ANY);

sub randpick {
    return $_[rand(@_)];
}

sub enhance {
    my $word = shift;
    return "\cC04☭" . uc $word . "☭\cC";
}

sub eggsIs {
    my $category = shift;
    my @args = @_;

    my $retval = systemx(EXIT_ANY, "is" . $category, @args);
    return $retval == 0;
}

sub isKing {
    my $user = shift;
    return eggsIs("King", $user);
}

sub isInBadStanding {
    my $user = shift;
    return eggsIs("InBadStanding", $user);
}

my %aliases = map { (lc($_) => 1) } split("\n", `allAliases`);

my @prefixes = ('kapi-', 'kammerat ', 'bolsjevik ');
my @suffixes = (qw(ski sky ov chenko));

sub commify {
    my $word = shift;
    my $is_name = $aliases{lc $word};

    my $prefix = '';
    my $suffix = '';
    if ($is_name) {
        my $user = $word;

        if (isKing($user)) { $prefix = 'formand '; }
        elsif (isInBadStanding($user)) { $prefix = 'klasseforrædder '; }
        else { $prefix = randpick(@prefixes); }

        $suffix = randpick(@suffixes);
    }

    my $result = $prefix . $word . $suffix;

    my $chance = rand();
    if ($is_name || $chance < 0.20) {
        $result = enhance($result);
    } elsif ($chance < 0.05) {
        $result = "█" x length $word;
    }

    return $result;
}

my $max_tax_rate = 1.2;

sub tax {
    my $amount = shift;
    $amount =~ s/,/./g;

    return $amount if $amount > 1800 && $amount < 2100;

    my $tax = int(rand($amount * $max_tax_rate));
    return sprintf "%s (+ %d i skat til partiet)", $amount - $tax, $tax;
}

while (my $line = <STDIN>) {
    chomp $line;

    my $me = $line =~ m!^/me !i;
    $line = substr $line, 4 if $me;

    $line =~ s/\b(\S+)\b/commify($1)/ge;
    $line =~ s/(\d+[.,]\d+|\d{4,})/tax($1)/ge;

    my $meprefix = $me ? "/me " : "";
    say $meprefix . $line;
}
