#!/usr/bin/env perl
use 5.012;
use strict;
use warnings;
use utf8::all;
use IPC::System::Simple qw(systemx EXIT_ANY);
use Lingua::Translit;

sub randpick {
    return $_[rand(@_)];
}

sub enhance {
    my $word = shift;
    return "\cC04☭" . uc $word . "☭\cC\cB\cB";
}

sub eggsIs {
    my $category = shift;
    my @args = @_;

    my $retval = systemx(EXIT_ANY, "is" . $category, @args);
    return $retval == 0;
}

sub isKing {
    my $user = shift;
    return eggsIs("King", $user);
}

sub isInBadStanding {
    my $user = shift;
    return eggsIs("InBadStanding", $user);
}

my %aliases = map { (lc($_) => 1) } split("\n", `allAliases`);

my @prefixes = ('kapi-', 'kammerat ', 'bolsjevik ');
my @suffixes = (qw(ski sky ov chenko));

sub commify {
    my $word = shift;
    my $is_name = $aliases{lc $word};

    my $result = $word;
    my $prefix = '';
    my $suffix = '';
    if ($is_name) {
        my $user = $word;

        if (isKing($user)) { $prefix = 'formand '; }
        elsif (isInBadStanding($user)) { $prefix = 'klasseforrædder '; }
        else { $prefix = randpick(@prefixes); }

        $suffix = randpick(@suffixes);
    }

    $result = $prefix . $result . $suffix;

    if (rand() < 0.10) {
        my $tr = Lingua::Translit->new('ISO 9');
        $result = $tr->translit_reverse( $result );
    }

    if ($is_name || (rand() < 0.20 && length $word > 3)) {
        $result = enhance($result);
    } elsif (rand() < 0.05) {
        $result = "█" x length $word;
    }

    return $result;
}

my $max_tax_rate = 1.2;

sub tax {
    my $amount = shift;
    $amount =~ s/,/./g;

    return $amount if $amount >= 1800 && $amount <= 2100;

    my $tax = int(rand($amount * $max_tax_rate));
    return sprintf "%s (+ %d i partiskat)", $amount - $tax, $tax;
}

while (my $line = <STDIN>) {
    chomp $line;

    my $me = $line =~ m!^/me !i;
    $line = substr $line, 4 if $me;

    $line =~ s/købe/dele/ig;
    $line =~ s/købte/delte/ig;
    $line =~ s/frokostkassen/enhance('partiet')/egi;
    $line =~ s/\b(\S+)\b/commify($1)/ge;
    $line =~ s/(\d+[.,]\d+|\d{4,})/tax($1)/ge;

    my $meprefix = $me ? "/me " : "";
    say $meprefix . $line;
}
