#!/usr/bin/env perl
use 5.020;
use warnings;
use utf8::all;
use IO::All -utf8;
use Env qw/CONCIEGGS_DB_DIR/;

my %aliases = map { (lc($_) => 1) } split("\n", `allAliases`);
sub twitterfy_name {
    my $name = shift;
    return $aliases{lc($name)} ? "@" . lc($name) : $name;
}

my @verbs = io("$CONCIEGGS_DB_DIR/ordbog-dansk-udsagnsord")->slurp;
my @nouns = io("$CONCIEGGS_DB_DIR/ordbog-dansk-navneord")->slurp;
chomp(@verbs);
chomp(@nouns);

sub random_base {
    my $list = shift;

    my $i = int(rand() * @$list);
    $i -= 1 while $i > 0 && $list->[$i] =~ qr/^\Q$list->[$i-1]\E/;
    return $list->[$i];
}

sub hashtag {
    return sprintf("#%s%s", random_base(\@verbs), random_base(\@nouns));
}

while (my $line = <>) {
    chomp($line);

    # Twitterfy names
    $line =~ s/\b(\S*)\b/twitterfy_name($1)/ge;
    $line =~ s/((?:^|\s)\@\S*?)[:,](\s)/$1$2/g;

    # Twitterfy links
    $line =~ s{https?://\S*?([.,]?(?:\s|$))}{https://t.co/VSn06APogV$1}g;

    # Add hashtags
    $line .= " " . hashtag() for (0 .. 3*rand());

    say $line;
}
