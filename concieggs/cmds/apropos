#!/usr/bin/env perl
#
# Brug: kædeviden [n] [emne], hvor n er 1-5. Begge argumenter er valgfri.
use 5.020;
use strict;
use warnings;
use utf8::all;

use Env qw(EGGS_USER);
use EggsML::EggsPI text => ['autoviden'];

sub max { $_[$_[0] < $_[1]] }
sub min { $_[$_[0] > $_[1]] }
sub bound { max(shift, min(shift, shift)) }

unless (@ARGV) {
    say "$EGGS_USER: Apropos hvad?";
    exit;
}

my $antal = $ARGV[0] =~ /^\d+$/ ? bound(1, 5, shift @ARGV) : 2;

sub slut_prut {
    say "Og så er jeg vist løbet tør for spændende ting at sige om dét.";
    exit(0);
}

my $hvad = join ' ', @ARGV;
my $viden = autoviden($hvad);
while ($antal-- > 0) {
    say $viden;

    my @interessant = grep { length $_ > 6 } split /[\s\.,!?]+/, $viden;
    slut_prut unless @interessant;

    my $hvad_mere = $interessant[rand @interessant];
    slut_prut if $hvad eq $hvad_mere;

    my $mere_viden = autoviden($hvad_mere);
    slut_prut if $viden eq $mere_viden;

    $hvad = $hvad_mere;
    $viden = "Apropos $hvad... " . $mere_viden;
}
