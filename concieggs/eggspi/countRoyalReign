#!/usr/bin/env perl
#
# Count the amount of time a king has reigned in seconds.

use 5.020;
use warnings;
no warnings 'experimental::smartmatch';

use EggsML::EggsPI list => [qw(aliases dbRead)];

unless (@ARGV > 0) {
    say "0";
    exit(0);
}

my ($user) = @ARGV;
my @aliases = map { fc } aliases($user);
my @kinglist = grep { m/^\d+\s/ } dbRead('eggskinglist');

my $last_ts = undef;
my $is_king = 0;
my $count = 0;
for my $line (@kinglist) {
    my ($ts, $name) = $line =~ m/^(\d+)\s+(.*)$/;

    if ($is_king) {
        $count += $ts - $last_ts;
        $is_king = 0;
    }

    if (fc $name ~~ @aliases) {
        $last_ts = $ts;
        $is_king = 1;
    }
}

$count += time - $last_ts if $is_king;

say $count;
