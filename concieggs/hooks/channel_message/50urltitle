#!/usr/bin/env perl
use 5.012;
use warnings;
use utf8::all;

use Env qw/EGGS_USER EGGS_BODY/;

my @links = $EGGS_BODY =~ m{((?:https?://|www\.)\S+?)[.,;!?]*(?:\s|$)}g;

if (@links) {
    # Load the modules lazily, to save time in the non-matching case
    eval <<EOU;
    use utf8::all;
    use WWW::Mechanize;
    use Sys::SigAction qw/timeout_call/;
EOU

    my $mech = WWW::Mechanize->new( agent => 'ircbot:concieggs:1.0 (by reddit.com/u/Sebbe)', timeout => 5 );
    local $| = 1;

    for my $link (@links) {
        print STDERR "Henter siden '$link' for at finde titlen...\n";
        next if (timeout_call(5, sub { $mech->get($link); }));
        next unless $mech->title;
        printf("%s: %s\n", $mech->title, $link);
    }
}
