#!/usr/bin/env perl
use 5.012;
use warnings;

use Env qw/EGGS_BODY/;

my @links = $EGGS_BODY =~ m{https?://(?:\w+\.)?reddit\.com/r/[\w\d]+/comments/\S+}g;

if (@links) {
    # Load the modules lazily, to save time in the non-matching case
    eval <<EOU;
    use LWP::UserAgent;
    use JSON qw/from_json/;
    use URI;
EOU

    my $ua = LWP::UserAgent->new;
    $ua->agent('ircbot:concieggs:1.0 (by /u/Sebbe)');

    for my $link (@links) {
        my $uri = URI->new($link);
        $uri->path($uri->path() . ".json?raw_json=1");
        my $json = $ua->get($uri)->decoded_content;
        my $data = from_json($json);

        my $elm = $data->[0]->{data}->{children}->[0]->{data};
        my $url = $elm->{is_self} ? "http://reddit.com/" . $elm->{permalink} : $elm->{url};
        printf("%s: %s (score: %d)\n", $elm->{title}, $url, $elm->{score});
    }
}
